(ns wits.blog.views
  (:use hiccup.element
        [hiccup.util :only [escape-html]]
        [hiccup.page :only [html5 include-css include-js]]
        [wits.web.html :only [sections html->hiccup html->enlive]]
        [markdown.core :only [md-to-html-string]]
        [wits.util :only [-#> -#>>]])
  (:require [markdown.core :as md]
            [wits.web.pages :as pages]
            [wits.web.html :as html]
            [wits.web.pjax :as pjax])
  (:import java.text.SimpleDateFormat))

(defn add-code-tags
  "The stuff generated by markdown doesn't explicitly label the code sections.
   Thus, it needs to be added manually so we can style it."
  [content]
  (map
    (fn [el]
      (if (html/tagged? :pre el)
        [:div.code-block el]
        el))
    content))

(defn prepare-blog-content
  [content]
  (-> content
    escape-html
    md-to-html-string
    html->hiccup
    add-code-tags))

(defn blog-view
  "Creates a Hiccup structure for a blog view."
  [{:keys [title date content]}]
  [:div.blog
   (sections
     :title title
     :date (->
             (SimpleDateFormat. "MMMM d, yyyy")
             (.format date))
     :content content)])

(defn full-blog
  "Creates a view of a full blog."
  [blog]
  (-> blog
    (update-in [:content] prepare-blog-content)
    blog-view))

(defn truncate-by-paragraphs
  "Returns the blog contents truncated to some number of paragraphs"
  [blog-content number-of-paragraphs]
  (->>
    blog-content
    (filter (-#> html/tag (= :p)))
    (take number-of-paragraphs)))

(defn blog-link
  ([link-contents blog]
   (blog-link [] link-contents blog))
  ([classes link-contents blog]
    (link-to
      {:class (->> classes (interpose " ") (apply str))}
      (str "/blog/entries/" (:url blog)) link-contents)))

(defn blog-preview
  "Creates a view of a preview of a blog for the blog roll."
  [blog]
  (-> blog
    (update-in [:content] prepare-blog-content)
    (update-in [:content] truncate-by-paragraphs 3)
    (update-in [:title] blog-link blog)
    (update-in [:content] concat [[:div.read-more (blog-link "read more..." blog)]])
    blog-view))

(def blog-css
  ["/css/blog.css"
   "/css/syntaxHighlighter.css"])

(def blog-js
  ["/js/lib/syntax-highlighter/brushes/shBrushClojure.js"
   "/js/lib/syntax-highlighter/brushes/shBrushCoffeeScript.js"
   "/js/lib/syntax-highlighter/brushes/shBrushXml.js"
   "/js/lib/syntax-highlighter/brushes/shBrushJScript.js"
   "/js/lib/syntax-highlighter/brushes/shBrushPython.js"
   "/js/lib/syntax-highlighter/brushes/shBrushJava.js"])

(def base-blog-page
  {:css
   blog-css

   :js
   blog-js})

(defn all-blogs
  [blogs pjax?]
  (pjax/page
    pages/main pjax?
    (merge
      base-blog-page
      {:title "Blog"
       :content
       [:div.page
        [:div.blogs
         (->> blogs
           (map blog-preview)
           (interpose html/content-separator))]]})))

(defn blog-page
  [{:keys [blogs previous-page next-page]} pjax?]
  (pjax/page
    pages/main pjax?
    (merge
      base-blog-page
      {:title "Blog"
       :content
       [:div.page
        [:div.blogs
         (->> blogs
           (map blog-preview)
           (interpose html/content-separator))]
        [:div.pagination-links
         (when previous-page (link-to {:class "previous"} (str "/blog/page/" previous-page) "previous page"))
         (when next-page (link-to {:class "next"} (str "/blog/page/" next-page) "next page"))]]})))

(defn blog-entry
  [blog pjax?]
  (pjax/page
    pages/main pjax?
    (merge
      base-blog-page
      {:title (blog :title)
       :content (full-blog blog)

       ; I should really move this into an external script
       ; but whatever #WorstDev
       :script
       "$(function() {
       SyntaxHighlighter.highlight();
       });"})))
